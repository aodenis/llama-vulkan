#version 450

#extension GL_GOOGLE_include_directive:enable

#define LOCAL_SUM_BITS BACKLOG_BITS

#include "common.glsl"

layout (binding = 0) buffer writeonly OutBuffer {
    float values[]; // [Z][DIM]
} outp;

layout (binding = 1) buffer readonly VCacheBuffer {
    float16_t values[]; // [BACKLOG][DIM]
} vcache;

layout (binding = 2) buffer readonly AttnBuffer {
    float values[]; // [Z][BACKLOG][HEAD_COUNT]
} attn;

#ifdef USE_SPEVAR
layout (local_size_x_id = SOFTMAX_HEAD_PER_WAVEFRONT_CID, local_size_y_id = BACKLOG_CID, local_size_z = 1) in;
#else
layout (local_size_x = SOFTMAX_HEAD_PER_WAVEFRONT, local_size_y = BACKLOG, local_size_z = 1) in;
#endif

void main()
{
    const uint v_row_id = gl_GlobalInvocationID.x;
    const uint local_v_row_id = gl_LocalInvocationID.x;
    const uint backlog_id = gl_GlobalInvocationID.y;
    const uint z_id = gl_GlobalInvocationID.z;

    float this_match = 0.;
    if (v_row_id < DIM) {
        this_match = float(vcache.values[backlog_id * DIM + v_row_id]) * attn.values[z_id * BACKLOG * HEAD_COUNT + backlog_id * HEAD_COUNT + (v_row_id / ROT)];
    }
    const float total_match = local_sum(local_v_row_id, backlog_id, this_match);
    if (v_row_id < DIM) {
        outp.values[z_id * DIM + v_row_id] = total_match;
    }
}
